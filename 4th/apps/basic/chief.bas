2 REM PRINT TAB(30) "CHIEF"
4 REM PRINT TAB(15) "CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
6 PRINT:PRINT:PRINT
10 PRINT "I AM CHIEF NUMBERS FREEK, THE GREAT INDIAN MATH GOD."
20 INPUT "ARE YOU READY TO TAKE THE TEST YOU CALLED ME OUT FOR (1=yes, 0=no)? ";A
40 IF A THEN GOTO 60
50 PRINT "SHUT UP, PALE FACE WITH WISE TONGUE."
60 PRINT "TAKE A NUMBER AND MULTIPLY BY 1000. ADD 3000."
65 PRINT "DIVIDE THIS NUMBER BY 5 AND MULTIPLY BY 8."
70 PRINT "DIVIDE BY 5 AND ADD 5000. SUBTRACT 1000."
80 INPUT " - WHAT DO YOU HAVE? ";B
100 LET C = (((((((B+1000)-5000)*5))/8)*5)-3000)/1000
110 PRINT "I BET YOUR NUMBER WAS ";C;
120 INPUT ". AM I RIGHT (1=yes, 0=no)? ";D
130 IF D THEN GOTO 510
140 PRINT "WHAT WAS YOUR ORIGINAL NUMBER";
150 INPUT K
155 LET F=(K*1000)+3000
160 LET G=F/5
170 LET H=G*8
180 LET I=(H/5)+5000
190 LET J=I-1000
200 PRINT "SO YOU THINK YOU'RE SO SMART, EH?"
210 PRINT "NOW WATCH."
230 PRINT K;" TIMES 1000, PLUS 3000 EQUALS ";F;"."
235 PRINT "THIS DIVIDED BY 5 EQUALS ";G;". THIS TIMES 8 EQUALS ";H;"."
240 PRINT "IF WE DIVIDE BY 5 AND ADD 5000, WE GET ";I;", WHICH,"
250 PRINT "MINUS 1000, EQUALS ";J;"."
260 INPUT "NOW DO YOU BELIEVE ME (1=yes, 0=no)? ";Z
290 IF Z THEN GOTO 510
295 PRINT "YOU HAVE MADE ME MAD!!!"
300 PRINT "THERE MUST BE A GREAT LIGHTNING BOLT!"
310 PRINT:PRINT
330 X=30
340 FOR Y=1 TO X : PRINT " "; : NEXT Y : PRINT "X X"
350 X=X-1: IF X>21 THEN GOTO 340
360 FOR Y=1 TO 21 : PRINT " "; : NEXT Y : PRINT "X XXX"
370 FOR Y=1 TO 20 : PRINT " "; : NEXT Y : PRINT "X   X"
380 FOR Y=1 TO 19 : PRINT " "; : NEXT Y : PRINT  "XX X"
390 Y=20
400 FOR X=1 TO Y : PRINT " "; : NEXT X : PRINT "X X"
410 Y=Y-1 : IF Y>12 THEN GOTO 400
420 FOR Y=1 TO 12 : PRINT " "; : NEXT Y : PRINT  "XX"
430 FOR Y=1 TO 11 : PRINT " "; : NEXT Y : PRINT  "X"
440 FOR Y=1 TO 10 : PRINT " "; : NEXT Y : PRINT  "*"
450 PRINT: PRINT "#########################": PRINT
470 PRINT "I HOPE YOU BELIEVE ME NOW, FOR YOUR SAKE!!"
480 GOTO 520
510 PRINT "BYE!!!"
520 END
