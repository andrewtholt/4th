1 PRINT "COMBAT"
2 PRINT "CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
3 PRINT: PRINT: PRINT
4 PRINT "I AM AT WAR WITH YOU.": PRINT "WE HAVE 72000 SOLDIERS APIECE."
5 PRINT:PRINT "DISTRIBUTE YOUR FORCES."
6 PRINT "","ME","  YOU"
7 PRINT "ARMY",30000,
8 INPUT A
9 PRINT "NAVY",20000,
10 INPUT B
11 PRINT "A. F.",22000,
12 INPUT C
13 IF A+B+C>72000 THEN GOTO 5
14 D=30000
15 E=20000
16 F=22000
17 PRINT "YOU ATTACK FIRST. TYPE (1) FOR ARMY; (2) FOR NAVY;"
18 PRINT "AND (3) FOR AIR FORCE."
19 INPUT Y
20 IF Y>3 THEN GOTO 17
30 IF Y<1 THEN GOTO 17
40 PRINT "HOW MANY MEN"
50 INPUT X
60 IF X<0 THEN GOTO 40
70 GOTO Y*100
100 IF X>A THEN GOTO 40
105 IF X<A/3 THEN GOTO 120
110 IF X<2*A/3 THEN GOTO 150
115 GOTO 270
120 PRINT "YOU LOST ";X;" MEN FROM YOUR ARMY."
125 A=(A-X)
130 GOTO 500
150 PRINT "YOU LOST ";X/3;" MEN, BUT I LOST ";2*D/3
155 A=(A-X/3)
160 D=0
165 GOTO 500 
200 IF X>B THEN GOTO 40
210 IF X<E/3 THEN GOTO 230
215 IF X<2*E/3 THEN GOTO 250
220 GOTO 270
230 PRINT "YOUR ATTACK WAS STOPPED!"
232 B=(B-X)
235 GOTO 500
250 PRINT "YOU DESTROYED ";(2*E/3);" OF MY ARMY."
255 E=(E/3)
260 GOTO 500
270 PRINT "YOU SUNK ONE OF MY PATROL BOATS, BUT I WIPED OUT TWO"
275 PRINT "OF YOUR AIR FORCE BASES AND 3 ARMY BASES."
280 A=(A/3)
285 C=(C/3)
290 E=(2*E/3)
293 GOTO 500
300 IF X>C THEN GOTO 40
310 IF X<C/3 THEN GOTO 350
320 IF X<2*C/3 THEN GOTO 370
330 GOTO 380
350 PRINT "YOUR ATTACK WAS WIPED OUT."
355 C=(C-X)
360 GOTO 500
370 PRINT "WE HAD A DOGFIGHT. YOU WON - AND FINISHED YOUR MISSION."
375 D=(2*D/3)
377 E=(E/3)
378 F=(F/3)
379 GOTO 500
380 PRINT "YOU WIPED OUT ONE OF MY ARMY PATROLS, BUT I DESTROYED"
381 PRINT "TWO NAVY BASES AND BOMBED THREE ARMY BASES."
385 A=(A/4)
387 B=(B/3)
390 D=(2*D/3)
500 PRINT
501 PRINT "","YOU","ME"
510 PRINT "ARMY",A,D
520 PRINT "NAVY",B,E
530 PRINT "A. F.",C,F
1000 PRINT "WHAT IS YOUR NEXT MOVE?"
1010 PRINT "ARMY=1  NAVY=2  AIR FORCE=3"
1020 INPUT G
1030 PRINT "HOW MANY MEN"
1040 INPUT T
1045 IF T<0 THEN GOTO 1030
1050 GOTO (G*100)+1500
1600 IF T>A THEN GOTO 1030
1610 IF T<D/2 THEN GOTO 1630
1615 PRINT "YOU DESTROYED MY ARMY!"
1616 D=0
1617 GOTO 2000
1630 PRINT "I WIPED OUT YOUR ATTACK!"
1635 A=A-T
1640 GOTO 2000
1700 IF T>B THEN GOTO 1030 
1710 IF T<E/2 THEN GOTO 1750
1720 GOTO 1770
1750 PRINT "I SUNK TWO OF YOUR BATTLESHIPS, AND MY AIR FORCE"
1751 PRINT "WIPED OUT YOUR UNGAURDED CAPITOL."
1755 A=A/4
1760 B=B/2
1765 GOTO 2000
1770 PRINT "YOUR NAVY SHOT DOWN THREE OF MY XIII PLANES,"
1771 PRINT "AND SUNK THREE BATTLESHIPS."
1775 F=2*F/3
1780 E=(E/2)
1790 GOTO 2000
1800 IF T>C THEN GOTO 1030 
1810 IF T>F/2 THEN GOTO 1830 
1820 GOTO 1850
1830 PRINT "MY NAVY AND AIR FORCE IN A COMBINED ATTACK LEFT"
1831 PRINT "YOUR COUNTRY IN SHAMBLES."
1835 A=A/3
1837 B=B/3
1840 C=C/3
1845 GOTO 2000
1850 PRINT "ONE OF YOUR PLANES CRASHED INTO MY HOUSE. I AM DEAD."
1851 PRINT "MY COUNTRY FELL APART."
1860 GOTO 2010
2000 PRINT
2001 PRINT "FROM THE RESULTS OF BOTH OF YOUR ATTACKS,"
2002 IF A+B+C>3/2*(D+E+F) THEN GOTO 2010
2005 IF A+B+C<2/3*(D+E+F) THEN GOTO 2015
2006 PRINT "THE TREATY OF PARIS CONCLUDED THAT WE TAKE OUR"
2007 PRINT "RESPECTIVE COUNTRIES AND LIVE IN PEACE."
2008 GOTO 2020
2010 PRINT "YOU WON, OH! SHUCKS!!!!"
2012 GOTO 2020
2015 PRINT "YOU LOST-I CONQUERED YOUR COUNTRY.  IT SERVES YOU"
2016 PRINT "RIGHT FOR PLAYING THIS STUPID GAME!!!"
2020 END
