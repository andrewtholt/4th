10 REM PRINT TAB(34);"HI LO"
20 REM PRINT TAB(15);"CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
30 PRINT:PRINT:PRINT
100 PRINT "THIS IS THE GAME OF HI LO.":PRINT
110 PRINT "YOU WILL HAVE 6 TRIES TO GUESS THE AMOUNT OF MONEY IN THE"
120 PRINT "HI LO JACKPOT, WHICH IS BETWEEN 1 AND 100 DOLLARS.  IF YOU"
130 PRINT "GUESS THE AMOUNT, YOU WIN ALL THE MONEY IN THE JACKPOT!"
140 PRINT "THEN YOU GET ANOTHER CHANCE TO WIN MORE MONEY.  HOWEVER,"
150 PRINT "IF YOU DO NOT GUESS THE AMOUNT, THE GAME ENDS.":PRINT
160 R=0
170 B=0:PRINT
180 Y=RND(100)
200 INPUT "YOUR GUESS: ";A
220 B=B+1
230 IF A=Y THEN GOTO 300
240 IF A>Y THEN GOTO 270
250 PRINT "YOUR GUESS IS TOO LOW.":GOTO 280
270 PRINT "YOUR GUESS IS TOO HIGH."
280 PRINT:IF B<6 THEN GOTO 200
290 PRINT "YOU BLEW IT...TOO BAD...THE NUMBER WAS ";Y
295 R=0:GOTO 350
300 PRINT "GOT IT!!!!!!!!!!   YOU WIN ";Y;" DOLLARS."
310 R=R+Y
320 PRINT "YOUR TOTAL WINNINGS ARE NOW ";R;" DOLLARS."
350 PRINT:INPUT "PLAY AGAIN (YES=1 OR NO=0)? ";A
360 IF A THEN GOTO 170
380 PRINT:PRINT "SO LONG.  HOPE YOU ENJOYED YOURSELF!!!"
390 END