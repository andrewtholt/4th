10 PRINT "GAME OF ROCK, SCISSORS, PAPER"
20 PRINT "CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
25 PRINT:PRINT:PRINT
30 INPUT "HOW MANY GAMES ";Q
40 IF Q<11 THEN GOTO 60
50 PRINT "SORRY, BUT WE AREN'T ALLOWED TO PLAY THAT MANY.": GOTO 30
60 FOR G=1 TO Q
70 PRINT: PRINT "GAME NUMBER ";G
80 X=RND(3)+1
90 PRINT "3=ROCK...2=SCISSORS...1=PAPER"
100 INPUT "1...2...3...WHAT'S YOUR CHOICE ";K
110 IF K<1 THEN PRINT "INVALID.": GOTO 90
115 IF K>3 THEN PRINT "INVALID.": GOTO 90
120 PRINT "THIS IS MY CHOICE..."
130 GOTO 130+(10*X)
140 PRINT "...PAPER": GOTO 170
150 PRINT "...SCISSORS": GOTO 170
160 PRINT "...ROCK"
170 IF X=K THEN GOTO 250
180 IF X>K THEN GOTO 230
190 IF X=1 THEN GOTO 210
200 PRINT "YOU WIN!!!":H=H+1: GOTO 260
210 IF K#3 THEN GOTO 200
220 PRINT "WOW!  I WIN!!!":C=C+1:GOTO 260
230 IF K#1 THEN GOTO 220
235 IF X#3 THEN GOTO 220
240 GOTO 200
250 PRINT "TIE GAME.  NO WINNER."
260 NEXT
270 PRINT: PRINT "HERE IS THE FINAL GAME SCORE:"
280 PRINT "I HAVE WON ";C;" GAME(S)."
290 PRINT "YOU HAVE WON ";H;" GAME(S)."
300 PRINT "AND ";Q-(C+H);" GAME(S) ENDED IN A TIE."
310 PRINT: PRINT "THANKS FOR PLAYING!!"
320 END